#!/usr/bin/env tclsh
set version 1.1.0
package require tcltest
namespace import ::tcltest::*
source "raindrops.tcl"

proc fail_fast {} {
    return [expr {
        ![info exists ::env(RUN_ALL)]
        || [string is boolean -strict $::env(RUN_ALL)]
        && !$::env(RUN_ALL)
    }]
}

proc failed {} {
    return [expr {$::tcltest::numTests(Failed) > 0}]
}

if {[fail_fast]} {
    proc test args {
        if {[failed]} {::tcltest::configure -skip *}
        uplevel [list ::tcltest::test {*}$args]
    }
}

proc cleanupTests {} {
    set failed [failed]
    uplevel 1 ::tcltest::cleanupTests
    if {$failed} {exit 1}
}

if {$::argv0 eq [info script]} {

    set cases {
           1 1
           3 Pling
           5 Plang
           7 Plong
           6 Pling
           8 8
           9 Pling
          10 Plang
          14 Plong
          15 PlingPlang
          21 PlingPlong
          25 Plang
          27 Pling
          35 PlangPlong
          49 Plong
          52 52
         105 PlingPlangPlong
        3125 Plang
    }

    foreach {number result} $cases {
        test raindrops-[incr n] "
            sound for $number is $result
        " -body {
            raindrops $number
        } -returnCodes ok -result $result
    }

    cleanupTests
}
