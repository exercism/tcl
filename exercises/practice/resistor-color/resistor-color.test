#!/usr/bin/env tclsh
package require tcltest
namespace import ::tcltest::*
proc skip {args} {
    if {!([info exists ::env(RUN_ALL)] && $::env(RUN_ALL))} {
        ::tcltest::skip {*}$args
    }
}

# compare 2 lists for equal elements in the same order
proc orderedListsEqual {a b} {
    if {[llength $a] != [llength $b]} then {return false}
    foreach elemA $a elemB $b {
        if {$elemA ne $elemB} then {return false}
    }
    return true
}
customMatch orderedLists orderedListsEqual

############################################################
source "resistor-color.tcl"

############################################################
# test colour list
test resistor-colors "List of colors" -body {
    resistorColor::colors
} -returnCodes ok -match orderedLists -result {
    black brown red orange yellow green blue violet grey white
}

# test individual colours
set cases {
    resistor-color-ok-1   black   0
    resistor-color-ok-2   white   9
    resistor-color-ok-3   orange  3
    resistor-color-ok-4   brown   1
    resistor-color-ok-5   red     2
    resistor-color-ok-6   yellow  4
    resistor-color-ok-7   green   5
    resistor-color-ok-8   blue    6
    resistor-color-ok-9   violet  7
    resistor-color-ok-10  grey    8
}

skip resistor-color-ok-*
foreach {name color value} $cases {
    test $name "Color $color" \
        -body [list resistorColor::colorCode $color] \
        -returnCodes ok \
        -result $value
}

# additional track-specific tests: error conditions
set cases {
    resistor-color-err-1 "beige" "Unknown color"
    resistor-color-err-2 "bl*" "test for lsearch with glob"
}

skip resistor-color-err-*
foreach {name color desc} $cases {
    test $name $desc \
        -body [list resistorColor::colorCode $color] \
        -returnCodes error \
        -result "Invalid color: $color"
}

cleanupTests
